plugins {
    id "java"
    id "maven-publish"
    id "checkstyle"
    id "jacoco"
    id "signing"
    id "org.embulk.embulk-plugins" version "0.5.5"
    id "com.palantir.git-version" version "0.12.3"
}

repositories {
    mavenCentral()
}

group = "com.treasuredata.embulk.plugins"
description = "Loads records from Marketo."
version = {
    def baseVersion = "0.6.26"
    def troccoVersion = "0.1.1"
    def tag = "${baseVersion}-trocco-${troccoVersion}"
    def vd = versionDetails()
    if (vd.lastTag != "${tag}") {
        logger.warn "lastTag '${vd.lastTag}' is not '${tag}'"
    }
    if (vd.commitDistance == 0 && vd.lastTag ==~ /^[0-9]+\.[0-9]+\.[0-9]+([.-][.a-zA-Z0-9-]+)?/) {
        vd.lastTag
    } else {
        "0.0.0.${vd.gitHash}"
    }
}()

sourceCompatibility = 1.8
targetCompatibility = 1.8

def embulkVersion = '0.10.29'

java {
    withJavadocJar()
    withSourcesJar()
}

dependencies {
    compileOnly "org.embulk:embulk-api:$embulkVersion"
    compileOnly "org.embulk:embulk-spi:$embulkVersion"

    implementation('org.embulk:embulk-util-config:0.3.2') {
        // Conflict with Embulk Core
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-annotations'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-core'
        exclude group: 'com.fasterxml.jackson.core', module: 'jackson-databind'
        exclude group: 'com.fasterxml.jackson.datatype', module: 'jackson-datatype-jdk8'
        exclude group: 'javax.validation', module: 'validation-api'
    }

    implementation "org.embulk:embulk-util-json:0.1.1"
    implementation "org.embulk:embulk-util-text:0.1.1"
    implementation "org.embulk:embulk-util-timestamp:0.2.1"

    // Explicit dependencies for embulk-util-* that matches with Embulk
    implementation 'com.fasterxml.jackson.core:jackson-core:2.6.7'
    implementation 'com.fasterxml.jackson.core:jackson-annotations:2.6.7'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.6.7'
    implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jdk8:2.6.7'
    implementation 'javax.validation:validation-api:1.1.0.Final'
    implementation 'org.apache.bval:bval-jsr303:0.5'
    implementation 'org.embulk:embulk-base-restclient:0.10.1'
    implementation 'org.embulk:embulk-util-retryhelper-jetty94:0.9.0'

    implementation 'com.google.guava:guava:18.0'
    implementation "com.google.code.findbugs:annotations:3.0.1"
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation "javax.xml.bind:jaxb-api:2.2.11"
    implementation "com.sun.xml.bind:jaxb-core:2.2.11"
    implementation "com.sun.xml.bind:jaxb-impl:2.2.11"
    implementation "javax.activation:activation:1.1.1"

    compile 'com.google.guava:guava:18.0'
    compile  "com.google.code.findbugs:annotations:3.0.1"
    compile 'org.apache.commons:commons-lang3:3.4'
    compile 'org.apache.commons:commons-csv:1.8'

    testImplementation "junit:junit:4.+"
    testImplementation "org.embulk:embulk-core:$embulkVersion:tests"
    testImplementation "org.embulk:embulk-junit4:$embulkVersion"
    testImplementation "org.embulk:embulk-deps:$embulkVersion"
    testImplementation "org.mockito:mockito-core:2.+"
}

embulkPlugin {
    mainClass = "org.embulk.input.marketo.MarketoInputPlugin"
    category = "input"
    type = "marketo"
}

publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = project.name

            from components.java  // Must be "components.java". The dependency modification works only for it.
            // javadocJar and sourcesJar are added by java.withJavadocJar() and java.withSourcesJar() above.
            // See: https://docs.gradle.org/current/javadoc/org/gradle/api/plugins/JavaPluginExtension.html

            pom {  // https://central.sonatype.org/pages/requirements.html
                packaging "jar"

                name = project.name
                description = project.description
                url = "https://www.embulk.org/"

                licenses {
                    license {
                        // http://central.sonatype.org/pages/requirements.html#license-information
                        name = "MIT License"
                        url = "http://www.opensource.org/licenses/mit-license.php"
                    }
                }

                developers {
                    developer {
                        name = "Treasure Data"
                        email = "dev@treasure-data.com"
                    }
                }

                scm {
                    connection = "scm:git:git://github.com/treasure-data/embulk-input-marketo.git"
                    developerConnection = "scm:git:git@github.com:treasure-data/embulk-input-marketo.git"
                    url = "https://github.com/treasure-data/embulk-input-marketo"
                }
            }
        }
    }

    repositories {
        maven {  // publishMavenPublicationToMavenCentralRepository
            name = "mavenCentral"
            if (project.version.endsWith("-SNAPSHOT")) {
                url "https://oss.sonatype.org/content/repositories/snapshots"
            } else {
                url "https://oss.sonatype.org/service/local/staging/deploy/maven2"
            }

            credentials {
                username = project.hasProperty("ossrhUsername") ? ossrhUsername : ""
                password = project.hasProperty("ossrhPassword") ? ossrhPassword : ""
            }
        }
    }
}

signing {
    sign publishing.publications.maven
}

gem {
    authors = ["uu59", "yoshihara", "taikhuu", "Treasure Data"]
    email = ["k@uu59.org", "h.yoshihara@everyleaf.com", "tai@treasuredata.com", "dev@treasure-data.com"]
    summary = "Marketo input plugin for Embulk"
    homepage = "https://github.com/treasure-data/embulk-input-marketo"
    licenses = ["MIT"]
}

gemPush {
    host = "https://rubygems.org"
}

javadoc {
    options {
        locale = 'en_US'
        encoding = 'UTF-8'
    }
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}

jacocoTestReport {
    group = "Reporting"
    reports {
        xml.enabled false
        csv.enabled false
        html.destination file("${buildDir}/reports/coverage")
    }
}
